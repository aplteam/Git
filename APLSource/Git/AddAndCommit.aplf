 r←AddAndCommit(y msg);nm;m;project;buff;rc;⎕TRAP;cb;path;cmd
⍝ Performs an "Add" followed by a "Commit".\\
⍝ If the current branch is "main" then `msg` **_must not_** be empty and should carry a meaningful message.
⍝ Otherwise `msg` may be empty but will become three dots (`...`).\\
⍝ `y` must be one of:
⍝   * Fully qualfied name of a namespace that is an opened Cider project
⍝   * Fully qualified path to a Git-managed project
⍝ Returns a two-element vector:
⍝  1. The name of the branch
⍝  2. The hash of the commit as a simple text string
 ⎕TRAP←(811 'E' '⎕DMX.EM ⎕signal 811')(0 'N')
 path←U.GetProjectPath y
 cb←CurrentBranch path
 nm←~(⊂cb)∊'master' 'main'
 ('You must provide a meaningful message in the "',cb,'" branch')⎕SIGNAL 11/⍨(0=nm)∧0=≢msg
 m←msg,(nm∧0=≢msg)/'...'
 m←U.MassageMsg m
 rc←Add path
 cmd←'commit -m "',m,'"'
 (rc msg buff)←path U.RunGitCommand cmd
 msg U.Assert 0=rc
 r←buff
⍝Done
