 r←{noOf}Log y;buff;cmd;msg;rc;⎕TRAP;path
⍝ Returns a matrix with four columns and zero to many rows:\\
⍝ 1. Hash of the commit
⍝ 2. User ID
⍝ 3. Timestamp
⍝ 4. Message\\
⍝ With the optional `noOf` you can limit the result to the last (youngest) `noOf` commits.\\
⍝ `y` must be one of:
⍝   * Fully qualfied name of a namespace  that is an opened Cider project
⍝   * Fully qualified path to a Git-managed project
 ⎕TRAP←(811 'E' '⎕DMX.EM ⎕signal 811')(0 'N')
 noOf←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noOf'
 path←U.GetProjectPath y
 'Invalid right argument'⎕SIGNAL 11/⍨1≠≡,noOf
 'Invalid right argument'⎕SIGNAL 11/⍨~(⎕DR noOf)∊83 163 323
 :If 0=noOf
     cmd←'log --pretty=format:"',U.GetLogFormat,'"'
 :Else
     cmd←'log -',(⍕|noOf),' --pretty=format:"',U.GetLogFormat,'"'
 :EndIf
 (rc msg buff)←path U.RunGitCommand cmd
 msg U.Assert 0=rc
 :If 0=≢buff
     r←0 4⍴''
 :Else
     r←↑U.FormatLog buff
 :EndIf
⍝Done
