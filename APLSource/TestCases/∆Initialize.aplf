 {r}←∆Initialize path;rc;more;buff;msg2;more2
⍝ Makes `path` a Git Repository and commits what's there as "Initial commit".\\
⍝ Always returns ⍬.
 (rc more buff)←G.U.RunGitCommand path 'init --initial-branch=main'
 G.U.Assert 0=rc
 (⊂G.U.GetGitAttributes)⎕NPUT path,'/.gitattributes'
 (rc more buff)← G.U.RunGitCommand path 'add .'
 :If 0≠rc
     more2←(⎕UCS 13)(≠⊆⊢)more
 :AndIf ~∧/⊃∨/'warning: LF will be replaced by CRLF' 'The file will have its original line endings'{⍺∘≡¨(≢⍺)↑¨⍵}¨⊂more2
     more G.U.Assert 0
 :EndIf
 (rc more buff)←G.U.RunGitCommand path 'commit -m "Initial commit"'
 G.U.Assert 0=rc
 r←⍬
⍝Done
