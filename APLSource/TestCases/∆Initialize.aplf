 {r}←∆Initialize folder;rc;more;buff;msg2;more2
⍝ Makes `folder` a Git Repository and commits what's there as "Initial commit".\\
⍝ Always returns ⍬.
 (rc more buff)←folder G.U.RunGitCommand'init --initial-branch=main'
 G.U.Assert 0=rc
 (⊂G.U.GetGitAttributes)⎕NPUT folder,'/.gitattributes'
 (rc more buff)←folder G.U.RunGitCommand'add .'
 :If 0≠rc
     more2←(⎕UCS 13)(≠⊆⊢)more
 :AndIf ~∧/⊃∨/'warning: LF will be replaced by CRLF' 'The file will have its original line endings'{⍺∘≡¨(≢⍺)↑¨⍵}¨⊂more2
     more G.U.Assert 0
 :EndIf
 (rc more buff)←folder G.U.RunGitCommand'commit -m "Initial commit"'
 G.U.Assert 0=rc
 r←⍬
⍝Done
